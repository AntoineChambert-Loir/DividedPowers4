import DividedPowers.DpAlgebra.Init
import DividedPowers.RatAlgebra
import DividedPowers.ForMathlib.WeightedHomogeneous
import DividedPowers.ForMathlib.GradedRingQuot

import Mathlib.Algebra.TrivSqZeroExt

noncomputable section

namespace DividedPowerAlgebra

open DirectSum Finset Function Ideal Ideal.Quotient MvPolynomial RingEquiv RingQuot TrivSqZeroExt 

section CommSemiring

variable (R M : Type _) [CommSemiring R] [AddCommMonoid M] [Module R M] 

variable [DecidableEq R] [DecidableEq M]

local instance : GradedAlgebra (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï)) :=
  weightedGradedAlgebra R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï)

theorem rel_isPureHomogeneous : 
    RelIsPureHomogeneous (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï)) (Rel R M) := by 
  intro a b h
  cases' h with a r n a m n a n a b
  . exact ‚ü®0, zero_mem _, zero_mem _‚ü©
  . use 0
    simp only [mem_weightedHomogeneousSubmodule, isWeightedHomogeneous_X, isWeightedHomogeneous_one]
  . use n
    simp only [mem_weightedHomogeneousSubmodule, Submodule.smul_mem, isWeightedHomogeneous_X]
  . use m + n
    constructor
    . apply IsWeightedHomogeneous.mul <;> simp only [isWeightedHomogeneous_X]
    . apply nsmul_mem
      simp only [mem_weightedHomogeneousSubmodule, isWeightedHomogeneous_X]
  . use n
    constructor
    . simp only [mem_weightedHomogeneousSubmodule, isWeightedHomogeneous_X]
    . apply Submodule.sum_mem
      intro c hc
      suffices n = c + (n - c) by
        nth_rewrite 2 [this]
        apply IsWeightedHomogeneous.mul <;> simp only [isWeightedHomogeneous_X]
      rw [mem_range, Nat.lt_succ_iff] at hc
      rw [Nat.add_sub_of_le hc]

theorem Rel_isHomogeneous : 
    RelIsHomogeneous (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï)) (Rel R M) := 
  RelIsHomogeneous_of_isPureHomogeneous _ (rel_isPureHomogeneous R M) Rel.rfl_zero

theorem RelI_isHomogeneous (R M : Type _) [CommRing R] [AddCommGroup M] [Module R M] 
    [DecidableEq R] [DecidableEq M] :
    (RelI R M).IsHomogeneous (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï)) := 
  IsHomogeneous_of_rel_isPureHomogeneous (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï)) 
    (Rel R M) (rel_isPureHomogeneous R M)
  
set_option linter.uppercaseLean3 false
#align divided_power_algebra.relI_is_homogeneous DividedPowerAlgebra.RelI_isHomogeneous

set_option linter.uppercaseLean3 true

/-- The graded submodules of `divided_power_algebra R M` -/
def grade (n : ‚Ñï) : Submodule R (DividedPowerAlgebra R M) :=
  quotSubmodule R (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï))
    (DividedPowerAlgebra.Rel R M) n
#align divided_power_algebra.grade DividedPowerAlgebra.grade

lemma mem_grade_iff (a : DividedPowerAlgebra R M) (n : ‚Ñï) : 
    a ‚àà grade R M n ‚Üî ‚àÉ (p : MvPolynomial (‚Ñï √ó M) R), 
      (p ‚àà weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï) n) ‚àß mk p = a := by
  simp only [grade, _root_.quotSubmodule, Submodule.mem_map]
  rfl

theorem one_mem : (1 : DividedPowerAlgebra R M) ‚àà grade R M 0 :=
  ‚ü®1, isWeightedHomogeneous_one R _, map_one _‚ü©
#align divided_power_algebra.one_mem DividedPowerAlgebra.one_mem

/-- The canonical decomposition of `divided_power_algebra R M` -/
def decomposition : DirectSum.Decomposition (M := DividedPowerAlgebra R M) (grade R M) := 
  _root_.quotDecomposition R (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï))
    (DividedPowerAlgebra.Rel R M) (Rel_isHomogeneous R M)
#align divided_power_algebra.decomposition DividedPowerAlgebra.decomposition

/-- The graded algebra structure on the divided power algebra-/
def GAlgebra : GradedAlgebra (DividedPowerAlgebra.grade R M) :=
  DirectSum.Decomposition_RingQuot R (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï)) 
    (DividedPowerAlgebra.Rel R M) (Rel_isHomogeneous R M)
#align divided_power_algebra.divided_power_galgebra DividedPowerAlgebra.GAlgebra

open MvPolynomial

-- Do we need both versions?
theorem dp_mem_grade (n : ‚Ñï) (m : M) : dp R n m ‚àà grade R M n :=
  ‚ü®X (n, m), isWeightedHomogeneous_X R _ (n, m), rfl‚ü©
#align divided_power_algebra.dp_mem_grade DividedPowerAlgebra.dp_mem_grade

/- theorem mk‚Çê_mem_grade (n : ‚Ñï) (m : M) : (mk‚Çê R (relI R M)) (X (n, m)) ‚àà grade R M n :=
  ‚ü®X (n, m), isWeightedHomogeneous_X R _ (n, m), rfl‚ü©
#align divided_power_algebra.mk‚Çê_mem_grade DividedPowerAlgebra.mk‚Çê_mem_grade
 -/

/-- degree of a product is sum of degrees -/
theorem mul_mem ‚¶Éi j : ‚Ñï‚¶Ñ {gi gj : DividedPowerAlgebra R M} (hi : gi ‚àà grade R M i)
    (hj : gj ‚àà grade R M j) : gi * gj ‚àà grade R M (i + j) :=
  (GAlgebra R M).toGradedMonoid.mul_mem hi hj
#align divided_power_algebra.mul_mem DividedPowerAlgebra.mul_mem

def decompose : DividedPowerAlgebra R M ‚Üí DirectSum ‚Ñï fun i : ‚Ñï => ‚Ü•(grade R M i) :=
  (GAlgebra R M).toDecomposition.decompose'
#align divided_power_algebra.decompose DividedPowerAlgebra.decompose

-- graded_algebra (grade R M )
instance : GradedAlgebra (DividedPowerAlgebra.grade R M) := GAlgebra R M

theorem mk_comp_toSupported :
    (@mk R M).comp ((Subalgebra.val _).comp (toSupported R)) = mk := by
  apply MvPolynomial.algHom_ext
  rintro ‚ü®n, m‚ü©
  simp only [AlgHom.coe_comp, mk‚Çê_eq_mk, Subalgebra.coe_val, Function.comp_apply, aeval_X,
    toSupported]
  split_ifs with h
  ¬∑ rfl
  ¬∑ simp only [not_lt, le_zero_iff] at h 
    rw [h, OneMemClass.coe_one, map_one]
    exact (dp_zero R m).symm

#align divided_power_algebra.mk‚Çê_comp_to_supported DividedPowerAlgebra.mk_comp_toSupported

theorem surjective_of_supported :
    Surjective ((@mk R M).comp (Subalgebra.val (supported R {nm : ‚Ñï √ó M | 0 < nm.1}))) := by
  intro f
  obtain ‚ü®p', hp'‚ü© := DividedPowerAlgebra.mk_surjective f
  use toSupported R p'
  rw [‚Üê AlgHom.comp_apply, AlgHom.comp_assoc, mk_comp_toSupported, ‚Üê hp']
#align divided_power_algebra.surjective_of_supported DividedPowerAlgebra.surjective_of_supported

theorem surjective_of_supported' {n : ‚Ñï} (p : grade R M n) :
    ‚àÉ q : supported R {nm : ‚Ñï √ó M | 0 < nm.1},
      IsWeightedHomogeneous Prod.fst q.1 n ‚àß (@mk R M) q.1 = ‚Üëp := by
  obtain ‚ü®p', hpn', hp'‚ü© := (mem_grade_iff R M _ _).mpr p.2
  use toSupported R p'
  constructor
  ¬∑ apply toSupported_isHomogeneous'
    exact hpn'
  erw [FunLike.congr_fun (mk_comp_toSupported R M) p', hp']
  -- TODO: write mk_comp_to_supported
#align divided_power_algebra.surjective_of_supported' DividedPowerAlgebra.surjective_of_supported'

theorem mem_grade_iff' {n : ‚Ñï} (p : DividedPowerAlgebra R M) :
    p ‚àà grade R M n ‚Üî ‚àÉ q : supported R {nm : ‚Ñï √ó M | 0 < nm.1},
      IsWeightedHomogeneous Prod.fst q.1 n ‚àß (@mk R M) q.1 = p := by
  constructor
  . intro hp
    rw [‚Üê Submodule.coe_mk p hp]
    apply surjective_of_supported'
  . rintro ‚ü®q, hq, rfl‚ü©
    rw [mem_grade_iff]
    exact ‚ü®q, hq, rfl‚ü©

/-- The canonical linear map `M ‚Üí‚Çó[R] divided_power_algebra R M`. -/
def Œπ : M ‚Üí‚Çó[R] DividedPowerAlgebra R M := {
  toFun     := fun m ‚Ü¶ dp R 1 m
  map_add'  := fun x y ‚Ü¶ by
    simp only [dp_add, sum_range_succ', sum_range_zero, zero_add, Nat.sub_zero, Nat.sub_self,
      dp_zero, mul_one, one_mul]
  map_smul' := fun r x ‚Ü¶ by
    simp only [dp_smul, pow_one, RingHom.id_apply] }
#align divided_power_algebra.Œπ DividedPowerAlgebra.Œπ

theorem Œπ_def (m : M) : Œπ R M m = dp R 1 m := rfl
#align divided_power_algebra.Œπ_def DividedPowerAlgebra.Œπ_def

/-
theorem mk_algHom_mvPolynomial_Œπ_eq_Œπ (m : M) : mk‚Çê R (relI R M) (X (1, m)) = Œπ R m :=
  rfl
#align divided_power_algebra.mk_alg_hom_mv_polynomial_Œπ_eq_Œπ 
  DividedPowerAlgebra.mk_algHom_mvPolynomial_Œπ_eq_Œπ

theorem mk_alg_hom_mv_polynomial_Œπ_eq_Œπ' (m : M) : dp R 1 m = Œπ R m :=
  rfl
#align divided_power_algebra.mk_alg_hom_mv_polynomial_Œπ_eq_Œπ'
  DividedPowerAlgebra.mk_alg_hom_mv_polynomial_Œπ_eq_Œπ'
-/

variable {M} 
@[simp] theorem Œπ_comp_lift {A : Type _} [CommRing A] [Algebra R A] {I : Ideal A} 
    (hI : DividedPowers I) {œÜ : M ‚Üí‚Çó[R] A} (hœÜ : ‚àÄ (m : M), œÜ m ‚àà I) : 
    (DividedPowerAlgebra.lift hI œÜ hœÜ).toLinearMap.comp (Œπ R M) = œÜ := by
  ext m
  simp only [LinearMap.coe_comp, Function.comp_apply, AlgHom.toLinearMap_apply, Œπ_def, 
    liftAlgHom_apply_dp]
  exact hI.dpow_one (hœÜ m)
#align divided_power_algebra.Œπ_comp_lift DividedPowerAlgebra.Œπ_comp_lift

@[simp] theorem lift_Œπ_apply {A : Type _} [CommRing A] [Algebra R A] {I : Ideal A} 
    (hI : DividedPowers I) (œÜ : M ‚Üí‚Çó[R] A) (hœÜ : ‚àÄ m, œÜ m ‚àà I) (x : M) : 
    lift hI œÜ hœÜ (Œπ R M x) = œÜ x := by
  conv_rhs => rw [‚Üê Œπ_comp_lift R hI hœÜ]; rfl
#align divided_power_algebra.lift_Œπ_apply DividedPowerAlgebra.lift_Œπ_apply


variable {R}

def HasGradedDpow {A : Type _} [CommSemiring A] [Algebra R A] 
    (ùíú : ‚Ñï ‚Üí Submodule R A) {I : Ideal A} (hI : DividedPowers I) :=
  ‚àÄ a ‚àà I, ‚àÄ (i : ‚Ñï) (_ : a ‚àà ùíú i) (n : ‚Ñï), hI.dpow n a ‚àà ùíú (n ‚Ä¢ i)
#align divided_power_algebra.has_graded_dpow DividedPowerAlgebra.HasGradedDpow

section DecidableEq

variable (R)

variable (S : Type _) [CommSemiring S] [Algebra R S] 

theorem liftAux_isHomogeneous {A : Type _} [CommSemiring A] [Algebra R A]
    [Algebra S A] [IsScalarTower R S A] (ùíú : ‚Ñï ‚Üí Submodule S A)
    [GradedAlgebra ùíú] (f : ‚Ñï √ó M ‚Üí A) (hf_zero : ‚àÄ m, f (0, m) = 1)
    (hf_smul : ‚àÄ (n : ‚Ñï) (r : R) (m : M), f ‚ü®n, r ‚Ä¢ m‚ü© = r ^ n ‚Ä¢ f ‚ü®n, m‚ü©)
    (hf_mul : ‚àÄ n p m, f ‚ü®n, m‚ü© * f ‚ü®p, m‚ü© = (n + p).choose n ‚Ä¢ f ‚ü®n + p, m‚ü©)
    (hf_add : ‚àÄ n u v, f ‚ü®n, u + v‚ü© = (range (n + 1)).sum fun x : ‚Ñï => f ‚ü®x, u‚ü© * f ‚ü®n - x, v‚ü©)
    (hf : ‚àÄ n m, f (n, m) ‚àà ùíú n) :
    GalgHom.IsHomogeneous (DividedPowerAlgebra.grade R M) ùíú
      (lift' f hf_zero hf_smul hf_mul hf_add) := by
  intro i a 
  simp only [mem_grade_iff]
  rintro ‚ü®p, hp, rfl‚ü©
  rw [lift'AlgHom_apply, p.as_sum, aeval_sum]
  apply _root_.sum_mem
  intro c hc
  rw [aeval_monomial, ‚Üê smul_eq_mul, algebraMap_smul A, algebra_compatible_smul S (coeff c p)]
  apply Submodule.smul_mem
  rw [‚Üê hp (mem_support_iff.mp hc)]
  exact Finsupp.prod.mem_grade _ _ _ _ fun ‚ü®n, m‚ü© _ => hf n m
#align divided_power_algebra.lift_aux_is_homogeneous DividedPowerAlgebra.liftAux_isHomogeneous

variable {R}

instance : GradedAlgebra (DividedPowerAlgebra.grade R M) := GAlgebra R M

theorem lift_isHomogeneous {A : Type _} [CommSemiring A] [Algebra R A] (ùíú : ‚Ñï ‚Üí Submodule R A) 
    [GradedAlgebra ùíú] {I : Ideal A} (hI : DividedPowers I) (hI' : HasGradedDpow ùíú hI)
    (œÜ : M ‚Üí‚Çó[R] A) (hœÜ : ‚àÄ m, œÜ m ‚àà I) (hœÜ' : ‚àÄ m, œÜ m ‚àà ùíú 1) :
    GalgHom.IsHomogeneous (DividedPowerAlgebra.grade R M) ùíú (lift hI œÜ hœÜ) := by
  apply liftAux_isHomogeneous
  intro n m
  simpa only [Algebra.id.smul_eq_mul, mul_one] using hI' (œÜ m) (hœÜ m) 1 (hœÜ' m) n
#align divided_power_algebra.lift_is_homogeneous DividedPowerAlgebra.lift_isHomogeneous

variable {N : Type _} [AddCommMonoid N] [DecidableEq S] [DecidableEq N] [Module R N] [Module S N] 
  [IsScalarTower R S N] [Algebra R (DividedPowerAlgebra S N)]
  [IsScalarTower R S (DividedPowerAlgebra S N)] 
  
theorem lift'_isHomogeneous (f : M ‚Üí‚Çó[R] N) :
    GalgHom.IsHomogeneous (DividedPowerAlgebra.grade R M) (DividedPowerAlgebra.grade S N)
      (LinearMap.lift R S f) := by
  apply liftAux_isHomogeneous R S (grade S N)
  exact (Œª (n : ‚Ñï) m => dp_mem_grade S N n (f m))
#align divided_power_algebra.lift'_is_homogeneous DividedPowerAlgebra.lift'_isHomogeneous

/- We need the projections (divided_power_algebra R M) ‚Üí grade R M n ,
more generally for graded algebras -/
variable (R M)

def proj' (n : ‚Ñï) : DividedPowerAlgebra R M ‚Üí‚Çó[R] grade R M n := proj (grade R M) n

#align divided_power_algebra.proj' DividedPowerAlgebra.proj'

theorem proj'_zero_one : (proj' R M 0) 1 = 1 := by
  rw [proj', proj, LinearMap.coe_mk, AddHom.coe_mk, decompose_one]
  rfl
#align divided_power_algebra.proj'_zero_one DividedPowerAlgebra.proj'_zero_one

theorem proj'_zero_mul (x y : DividedPowerAlgebra R M) :
    (proj' R M 0) (x * y) = (proj' R M 0) x * (proj' R M 0) y := by
  simp only [proj', ‚Üê projZeroRingHom'_apply, _root_.map_mul]
#align divided_power_algebra.proj'_zero_mul DividedPowerAlgebra.proj'_zero_mul

end DecidableEq