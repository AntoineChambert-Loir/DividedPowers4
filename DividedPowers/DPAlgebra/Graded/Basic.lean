/-
Copyright (c) 2024 Antoine Chambert-Loir, Mar√≠a In√©s de Frutos-Fern√°ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Chambert-Loir, Mar√≠a In√©s de Frutos-Fern√°ndez
-/
import DividedPowers.DPAlgebra.Init
import DividedPowers.ForMathlib.GradedRingQuot
import Mathlib.Algebra.MvPolynomial.CommRing

/-!
# The graded algebra structure on the universal divided power algebra

Let `R` be a ring and `M` be an `R`-module. We define the graded algebra structure on the universal
divided power algebra of `M`.


## Main definitions/results

* `DividedPowerAlgebra.grade`: the graded submodules of `DividedPowerAlgebra R M`.

* `DividedPowerAlgebra.decomposition`: the canonical decomposition of `DividedPowerAlgebra R M`
  as a sun of its graded components.

* `DividedPowerAlgebra.gradedAlgebra` : the graded algebra structure on `DividedPowerAlgebra R M`.

* `DividedPowerAlgebra.HasGradedDpow` : we say that a divided power algebra has a graded divided
  power structure if for every `n : ‚Ñï`, `hI.dpow n` sends elements of `ùíú i` to elements of
  `ùíú (n ‚Ä¢ i)`.

* `DividedPowerAlgebra.proj'` : the projection from `DividedPowerAlgebra R M` to the degree `n`
submodule `grade R M n`, as an `R`-linear map.

-/

noncomputable section

namespace DividedPowerAlgebra

open DirectSum Finset Function Ideal Ideal.Quotient MvPolynomial RingEquiv RingQuot

variable (R M : Type*) [CommSemiring R] [AddCommMonoid M] [Module R M]

/-- The weighted graded algebra structure on `MvPolynomial (‚Ñï √ó M) R`.-/
local instance : GradedAlgebra (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï)) :=
  weightedGradedAlgebra R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï)

theorem rel_isPureHomogeneous :
    Rel.IsPureHomogeneous (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï)) (Rel R M) := by
  intro a b h
  cases h with
  | rfl_zero => exact ‚ü®0, zero_mem _, zero_mem _‚ü©
  | zero =>
      use 0
      simp only [mem_weightedHomogeneousSubmodule, isWeightedHomogeneous_X,
        isWeightedHomogeneous_one, and_self]
  | @smul r n x =>
      use n
      simp only [mem_weightedHomogeneousSubmodule, isWeightedHomogeneous_X,
        Submodule.smul_mem, and_self]
  | @mul m n =>
      use m + n
      exact ‚ü®IsWeightedHomogeneous.mul (isWeightedHomogeneous_X _ _ _)
        (isWeightedHomogeneous_X _ _ _), nsmul_mem ((mem_weightedHomogeneousSubmodule _ _ _ _).mpr
        (isWeightedHomogeneous_X _ _ _)) _‚ü©
  | @add n =>
      use n
      refine ‚ü®(mem_weightedHomogeneousSubmodule _ _ _ _).mpr (isWeightedHomogeneous_X _ _ _), ?_‚ü©
      apply Submodule.sum_mem
      exact fun (c, d) hcd ‚Ü¶ mem_antidiagonal.mp hcd ‚ñ∏
        (isWeightedHomogeneous_X _ _ _).mul (isWeightedHomogeneous_X _ _ _)

theorem Rel_isHomogeneous :
    Rel.IsHomogeneous (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï)) (Rel R M) :=
  Rel.isHomogeneous_of_isPureHomogeneous (rel_isPureHomogeneous R M) Rel.rfl_zero

theorem RelI_isHomogeneous (R : Type*) [CommRing R] (M : Type*) [AddCommGroup M] [Module R M] :
    (RelI R M).IsHomogeneous (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï)) :=
  isHomogeneous_of_rel_isPureHomogeneous (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï))
    (Rel R M) (rel_isPureHomogeneous R M)

/-- The graded submodules of `DividedPowerAlgebra R M`. -/
def grade (n : ‚Ñï) : Submodule R (DividedPowerAlgebra R M) :=
  quotSubmodule R (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï))
    (DividedPowerAlgebra.Rel R M) n

variable {R M}
lemma mem_grade_iff {a : DividedPowerAlgebra R M} {n : ‚Ñï} :
    a ‚àà grade R M n ‚Üî ‚àÉ (p : MvPolynomial (‚Ñï √ó M) R),
      (p ‚àà weightedHomogeneousSubmodule R Prod.fst n) ‚àß mk p = a := by
  simp only [grade, _root_.quotSubmodule, Submodule.mem_map]; rfl

def mk' {p : MvPolynomial (‚Ñï √ó M) R} {n : ‚Ñï} (hp : IsWeightedHomogeneous Prod.fst p n) :
    grade R M n := ‚ü®mk p, mem_grade_iff.mp ‚ü®p, hp, rfl‚ü©‚ü©

lemma coe_mk' {p : MvPolynomial (‚Ñï √ó M) R} {n : ‚Ñï} (hp : IsWeightedHomogeneous Prod.fst p n) :
    (mk' hp) = mk p := rfl

variable (R M)

theorem one_mem : (1 : DividedPowerAlgebra R M) ‚àà grade R M 0 :=
  ‚ü®1, isWeightedHomogeneous_one R _, by simp [map_one]‚ü©

/-- The canonical decomposition of `DividedPowerAlgebra R M` -/
def decomposition : DirectSum.Decomposition (M := DividedPowerAlgebra R M) (grade R M) :=
  quotDecomposition (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï))
    (DividedPowerAlgebra.Rel R M) (Rel_isHomogeneous R M)

/-- The graded algebra structure on the divided power algebra-/
instance gradedAlgebra : GradedAlgebra (grade R M) :=
  quotGradedAlgebra (weightedHomogeneousSubmodule R (Prod.fst : ‚Ñï √ó M ‚Üí ‚Ñï))
    (DividedPowerAlgebra.Rel R M) (Rel_isHomogeneous R M)

open MvPolynomial

theorem dp_mem_grade (n : ‚Ñï) (m : M) : dp R n m ‚àà grade R M n :=
  ‚ü®X (n, m), isWeightedHomogeneous_X R _ (n, m), rfl‚ü©

/-- The degree of a product is equal to the sum of the degrees. -/
theorem mul_mem ‚¶Éi j : ‚Ñï‚¶Ñ {gi gj : DividedPowerAlgebra R M}
    (hi : gi ‚àà grade R M i) (hj : gj ‚àà grade R M j) : gi * gj ‚àà grade R M (i + j) :=
  (gradedAlgebra R M).toGradedMonoid.mul_mem hi hj

/-- The equivalence between `DividedPowerAlgebra R M` and the direct sum of `grade R M i`,
  where `i` runs over `‚Ñï`. -/
def decompose : DividedPowerAlgebra R M ‚Üí DirectSum ‚Ñï fun i : ‚Ñï => ‚Ü•(grade R M i) :=
  (gradedAlgebra R M).toDecomposition.decompose'

theorem mk_comp_toSupported :
    (@mk R M).comp ((Subalgebra.val _).comp (toSupported R)) = mk := by
  apply MvPolynomial.algHom_ext
  rintro ‚ü®n, m‚ü©
  simp only [AlgHom.coe_comp, Subalgebra.coe_val, Function.comp_apply, aeval_X, toSupported]
  split_ifs with h
  ¬∑ rfl
  ¬∑ simp only [not_lt, le_zero_iff] at h
    rw [h, OneMemClass.coe_one, map_one]
    exact (dp_zero R m).symm

theorem surjective_of_supported :
    Surjective ((@mk R M).comp (Subalgebra.val (supported R {nm : ‚Ñï √ó M | 0 < nm.1}))) := by
  intro f
  obtain ‚ü®p', hp'‚ü© := DividedPowerAlgebra.mk_surjective f
  use toSupported R p'
  rw [‚Üê AlgHom.comp_apply, AlgHom.comp_assoc, mk_comp_toSupported, ‚Üê hp']

variable {R M}

theorem surjective_of_supported' {n : ‚Ñï} (p : grade R M n) :
    ‚àÉ q : supported R {nm : ‚Ñï √ó M | 0 < nm.1},
      IsWeightedHomogeneous Prod.fst q.1 n ‚àß (@mk R M) q.1 = ‚Üëp := by
  obtain ‚ü®p', hpn', hp'‚ü© := mem_grade_iff.mpr p.2
  use toSupported R p'
  refine ‚ü®toSupported_isHomogeneous _ _ _ hpn', ?_‚ü©
  erw [DFunLike.congr_fun (mk_comp_toSupported R M) p', hp']

/-- We show that an element `p : DividedPowerAlgebra R M` belongs to the degree `n` submodule if
  and only if it is the image under `DividedPowerAlgebra.mk` of a homogeneous polynomial of
  degree `n` supported on `{nm : ‚Ñï √ó M | 0 < nm.1}`. -/
theorem mem_grade_iff' {n : ‚Ñï} (p : DividedPowerAlgebra R M) :
    p ‚àà grade R M n ‚Üî ‚àÉ q : supported R {nm : ‚Ñï √ó M | 0 < nm.1},
      IsWeightedHomogeneous Prod.fst q.1 n ‚àß (@mk R M) q.1 = p :=
  ‚ü®fun hp ‚Ü¶ Submodule.coe_mk p hp ‚ñ∏ surjective_of_supported' _, fun ‚ü®q, hq, hpq‚ü© ‚Ü¶  ‚ü®q, hq, hpq‚ü©‚ü©

/-- We say that a divided power algebra has a *graded* divided power structure if for every `n : ‚Ñï`,
  `hI.dpow n` sends elements of `ùíú i` to elements of `ùíú (n ‚Ä¢ i)`.  -/
def HasGradedDpow {A : Type*} [CommSemiring A] [Algebra R A]
    (ùíú : ‚Ñï ‚Üí Submodule R A) {I : Ideal A} (hI : DividedPowers I) : Prop :=
  ‚àÄ a ‚àà I, ‚àÄ (n i : ‚Ñï) (_ : a ‚àà ùíú i) , hI.dpow n a ‚àà ùíú (n ‚Ä¢ i)

section lift'

variable {S : Type*} [CommSemiring S] [Algebra R S]

theorem liftAux_isHomogeneous {A : Type*} [CommSemiring A] [Algebra R A] [Algebra S A]
    [IsScalarTower R S A] (ùíú : ‚Ñï ‚Üí Submodule S A) [GradedAlgebra ùíú] {f : ‚Ñï √ó M ‚Üí A}
    (hf_zero : ‚àÄ m, f (0, m) = 1)
    (hf_smul : ‚àÄ (n : ‚Ñï) (r : R) (m : M), f ‚ü®n, r ‚Ä¢ m‚ü© = r ^ n ‚Ä¢ f ‚ü®n, m‚ü©)
    (hf_mul : ‚àÄ n p m, f ‚ü®n, m‚ü© * f ‚ü®p, m‚ü© = (n + p).choose n ‚Ä¢ f ‚ü®n + p, m‚ü©)
    (hf_add : ‚àÄ n u v, f ‚ü®n, u + v‚ü© = (antidiagonal n).sum fun (x, y) => f ‚ü®x, u‚ü© * f ‚ü®y, v‚ü©)
    (hf : ‚àÄ n m, f (n, m) ‚àà ùíú n) :
    GAlgHom.IsHomogeneous (DividedPowerAlgebra.grade R M) ùíú (id : ‚Ñï ‚Üí ‚Ñï)
      (lift' hf_zero hf_smul hf_mul hf_add) := by
  intro i a
  rw [mem_grade_iff]
  rintro ‚ü®p, hp, rfl‚ü©
  rw [lift'_apply, p.as_sum, aeval_sum]
  apply _root_.sum_mem
  intro c hc
  rw [aeval_monomial, ‚Üê smul_eq_mul, algebraMap_smul A, algebra_compatible_smul S (coeff c p)]
  apply Submodule.smul_mem
  exact hp (mem_support_iff.mp hc) ‚ñ∏ Finsupp.prod_mem_grade fun ‚ü®n, m‚ü© _ => hf n m

theorem lift_isHomogeneous {A : Type*} [CommSemiring A] [Algebra R A] (ùíú : ‚Ñï ‚Üí Submodule R A)
    [GradedAlgebra ùíú] {I : Ideal A} (hI : DividedPowers I) (hI' : HasGradedDpow ùíú hI)
    (œÜ : M ‚Üí‚Çó[R] A) (hœÜ : ‚àÄ m, œÜ m ‚àà I) (hœÜ' : ‚àÄ m, œÜ m ‚àà ùíú 1) :
    GAlgHom.IsHomogeneous (DividedPowerAlgebra.grade R M) ùíú  (id : ‚Ñï ‚Üí ‚Ñï) (lift hI œÜ hœÜ) := by
  apply liftAux_isHomogeneous
  intro n m
  simpa [smul_eq_mul, mul_one] using hI' (œÜ m) (hœÜ m) n 1 (hœÜ' m)

variable {N : Type*} [AddCommMonoid N] [Module R N] [Module S N]
  [IsScalarTower R S N]

theorem lift'_isHomogeneous (f : M ‚Üí‚Çó[R] N) :
    GAlgHom.IsHomogeneous (DividedPowerAlgebra.grade R M) (DividedPowerAlgebra.grade S N)
      (id : ‚Ñï ‚Üí ‚Ñï) (DividedPowerAlgebra.LinearMap.lift S f) :=
  liftAux_isHomogeneous (grade S N) _ _ _ _ (Œª (n : ‚Ñï) m => dp_mem_grade S N n (f m))

end lift'

section proj'

variable (R M)

/-- The projection from `DividedPowerAlgebra R M` to the degree `n` submodule `grade R M n`,
  as an `R`-linear map-/
def proj' (n : ‚Ñï) : DividedPowerAlgebra R M ‚Üí‚Çó[R] grade R M n :=
  GradedAlgebra.proj' (grade R M) n

theorem proj'_zero_one : (proj' R M 0) 1 = 1 := by
  rw [proj', GradedAlgebra.proj', LinearMap.coe_mk, AddHom.coe_mk, decompose_one]; rfl

theorem proj'_zero_mul (x y : DividedPowerAlgebra R M) :
    (proj' R M 0) (x * y) = (proj' R M 0) x * (proj' R M 0) y := by
  simp only [proj', ‚Üê GradedAlgebra.proj'_zeroRingHom_apply, _root_.map_mul]

end proj'

end DividedPowerAlgebra
